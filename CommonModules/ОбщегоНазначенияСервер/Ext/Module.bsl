
// Процедура - Для всех коллекций очистить флаг учить
//
// Параметры:
//  обкИсключение - КоллекцииСсылка - "Учить" одновременно можно только одну коллекцию
//	по этой причине даннаю процедура снимает данный флаг всем элементам кроме одного (переданного)
//
Процедура ДляВсехКоллекцийОчиститьФлагУчить(обкИсключение) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Коллекции.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Коллекции КАК Коллекции
		|ГДЕ
		|	Коллекции.Учить
		|	И Коллекции.Ссылка <> &обкИсключение";
	
	Запрос.УстановитьПараметр("обкИсключение", обкИсключение);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		обк = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		обк.Учить = Ложь;
		обк.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Функция - Получить карточки учебной коллекции
// 
// Возвращаемое значение:
//  РезультатЗапроса - РезультатЗапроса - результат запроса
//  в который выбраны все карточки у которых родитель имеет знч "Учить" = Истина
//
Функция ПолучитьКарточкиУчебнойКоллекции() Экспорт
	
    КоллекцияКарточек = Справочники.Коллекции.НайтиПоРеквизиту("Учить", Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Карточки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Карточки КАК Карточки
		|ГДЕ
		|	Карточки.Владелец = &Коллекция
		|	И НЕ Карточки.ПометкаУдаления
		|	И НЕ Карточки.ЭтоГруппа";
	
	Если КоллекцияКарточек.БезЗнаю Тогда
		Запрос.Текст = Запрос.Текст + " И НЕ Карточки.Статус";
	КонецЕсли;

	Запрос.УстановитьПараметр("Коллекция", 	КоллекцияКарточек.Ссылка);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;

КонецФункции

// Функция - Получить основные данные карточки
//
// Параметры:
//  Карточка - 	 - 
// 
// Возвращаемое значение:
//  Ответ - Структура Из
//	*Вопрос - Строка - текст из поля Ссылка.ВопросТекст
//	*Ответ  - Строка - все ответы Ссылка.Ответы 
//  у которых "Верный" = Истина соединенные в строку через перенос строки
//
Функция ПолучитьОсновныеДанныеКарточки(Карточка) Экспорт
	
	Ответ = Новый Структура;
	Ответ.Вставить("Вопрос");
	Ответ.Вставить("Ответ");
	
	Ответы = Новый Массив;
	Для каждого ОтветКарточки Из Карточка.Ответы Цикл
	
		Если ОтветКарточки.Верный Тогда		
			Ответы.Добавить(ОтветКарточки.Текст);		
		КонецЕсли;
	
	КонецЦикла;
	
	Ответ.Вопрос = Карточка.ВопросТекст;
	Ответ.Ответ  = СтрСоединить(Ответы, Символы.ПС);
	
	Возврат Ответ;
	
КонецФункции

// Процедура - Установить статус карточки на сервере
//
// Параметры:
//  Карточка - КарточкаСсылка - ссылка на элемент справочника
//  Статус	 - Булево - Ложь - Не знаю, Истина - Знаю
//
Процедура УстановитьСтатусКарточкиНаСервере(Карточка, Статус) Экспорт
	
	Обк = Карточка.ПолучитьОбъект();
	Обк.Статус = Статус;
	Обк.Записать();
	
КонецПроцедуры
